import "main.ostw";

enum PlayerRole {
    CREW,
    IMPOSTER
}

globalvar Vector circleCenter = Vector(-208.7, 0.0, -16.4);
globalvar Corpse[] corpses = EmptyArray();

// Player Variables
playervar PlayerRole role;
playervar define hudInteractString = "";
playervar define hudReloadString = "";
playervar Task[] incompleteTasks;
playervar Task[] completeTasks = EmptyArray();
playervar Corpse closestCorpse;
playervar define canReportCorpse = false;

/* Initial setup for players.
 * Disables most abilities and ui elements.
 */
rule: "Player Setup"
Event.OnPlayerJoin
{
    DisableGameModeHud(EventPlayer());
    DisableKillFeed(EventPlayer());
    DisableMessages(EventPlayer());
    DisableSpectate(EventPlayer());
    DisableHeroHud(EventPlayer())
    DisableSpectateHUD(EventPlayer());
    DisableNameplates(EventPlayer());
    DisableRespawning(EventPlayer());
    DisableScoreboard(EventPlayer());
    SetUltimateAbilityEnabled(EventPlayer(), false);
    SetMeleeEnabled(EventPlayer(), false);
    SetCrouchEnabled(EventPlayer(), false);
    SetReloadEnabled(EventPlayer(), false);
    SetAbility1Enabled(EventPlayer(), false);
    SetAbility2Enabled(EventPlayer(), false);
    SetPrimaryFireEnabled(EventPlayer(), false);
    SetSecondaryFireEnabled(EventPlayer(), false);
    //SetDamageReceived(EventPlayer(), 0);
    //SetJumpEnabled(EventPlayer(), false);
    //SetMoveSpeed(EventPlayer(), 300);

    CreateHudText(EventPlayer(), Subheader: <"(X: <0>, Y: <1>, Z: <2>)", XOf(PositionOf(EventPlayer())), YOf(PositionOf(EventPlayer())), ZOf(PositionOf(EventPlayer()))>);
    ChaseVariableAtRate(EventPlayer().killCooldown, 0, 1.0, RateChaseReevaluation.None);
}

/* Puts an orb in the center of the map.
 */
void markCenter() {
    CreateEffect(AllPlayers(), Effect.Sphere, Color.Aqua, circleCenter, 1, EffectRev.None);
}

/* Calculates a position on the edge of a circle based on its center, radius, and the target 'slot' of the circle.
 */
Vector circlePosition(Vector center, ref define slot, ref define totalSlots, define radius) {
    define radians = (2 * Pi() * slot) / totalSlots;
    return Vector(XOf(center) + (radius * CosineFromRadians(radians)), YOf(center), ZOf(center) + (radius * SineFromRadians(radians)));
}

/* Place a player in the starting circle.
 */
void putPlayerInCircle(ref define player) {
    define pos = circlePosition(circleCenter, SlotOf(player), 10, 8.0)
    Teleport(AllPlayers()[SlotOf(player)], pos);
    SetFacing(AllPlayers()[SlotOf(player)], circleCenter - pos);
}

/* Set player status roles randomly per the imposter count.
 */
void assignRoles(ref define imposters) {
    AllPlayers().role = PlayerRole.CREW;
    ChooseArray(AllPlayers(), imposters).role = PlayerRole.IMPOSTER;
}

/* Give each player a selection of random tasks.
 * Tasks only contribute to the total task target if the player is Crew.
 */
void assignTasks(ref define player, ref define numTasks) {
    player.numTasks = numTasks;
    player.incompleteTasks = ChooseArray(global_tasks, numTasks);
    if (player.role == PlayerRole.CREW) {
        totalTaskCount += numTasks;
    }
}

/* Update the hud task strings.
 */
void updateHudStrings(ref define player) {
    player.hudTaskString = <"<0><1><2><3><4><5>",
        player.incompleteTasks.Length > 0 ? <"1. <0>\n", player.incompleteTasks[0].Name> : "",
        player.incompleteTasks.Length > 1 ? <"2. <0>\n", player.incompleteTasks[1].Name> : "",
        player.incompleteTasks.Length > 2 ? <"3. <0>\n", player.incompleteTasks[2].Name> : "",
        player.incompleteTasks.Length > 3 ? <"4. <0>\n", player.incompleteTasks[3].Name> : "",
        player.incompleteTasks.Length > 4 ? <"5. <0>\n", player.incompleteTasks[4].Name> : "",
        player.incompleteTasks.Length > 5 ? <"6. <0>\n", player.incompleteTasks[5].Name> : ""
    >;
    player.hudCompleteTaskString = <"<0><1><2><3><4><5>",
        player.completeTasks.Length > 0 ? <"1. <0>\n", player.completeTasks[0].Name> : "",
        player.completeTasks.Length > 1 ? <"2. <0>\n", player.completeTasks[1].Name> : "",
        player.completeTasks.Length > 2 ? <"3. <0>\n", player.completeTasks[2].Name> : "",
        player.completeTasks.Length > 3 ? <"4. <0>\n", player.completeTasks[3].Name> : "",
        player.completeTasks.Length > 4 ? <"5. <0>\n", player.completeTasks[4].Name> : "",
        player.completeTasks.Length > 5 ? <"6. <0>\n", player.completeTasks[5].Name> : ""
    >;
}

/* Per-tick update logic for crew.
 */
void crewUpdate(ref define player) {
    if (player.isTaskInProgress) {
        updateCrewTaskProgress(player);
    } else {
        findCrewNearestTask(player);
    }

}

/* Debug-only update logic.
 */
void debugUpdate(ref define player) {
    if (IsButtonHeld(player, Button.Ultimate)) {
        totalCompleteTaskCount++;
        checkEndGame();
    }
    if (IsButtonHeld(player, Button.Ability1)) {
        killPlayer(RandomValueInArray(AllLivingPlayers()));
    }
}

/* Check if task progress may continue or if it should cancel or complete.
 */
void updateCrewTaskProgress(ref define player) {
    // Still near enough to task to work on it, and holding the button?
    if (!IsButtonHeld(player, Button.Interact) || DistanceBetween(PositionOf(player), player.interactableTask.Position) > 3) {
        player.isTaskInProgress = false;
        StopChasingVariable(player.taskProgressTime);
        return;
    }
    // If task done, move it to the complete list
    if (player.taskProgressTime >= player.interactableTask.Duration) {
        player.isTaskInProgress = false;
        StopChasingVariable(player.taskProgressTime);
        ModifyVariable(player.completeTasks, Operation.AppendToArray, player.interactableTask);
        ModifyVariable(player.incompleteTasks, Operation.RemoveFromArrayByValue, player.interactableTask);
        totalCompleteTaskCount++;
        updateHudStrings(player);
        checkEndGame();
    }
}

/* Find the nearest task to the crew member.
 */
void findCrewNearestTask(ref define player) {
    player.hudInteractString = "";
    // Find first incomplete task within 3 meters
    for (define i=0; i < player.incompleteTasks.Length; i++) {
        if (DistanceBetween(PositionOf(player), player.incompleteTasks[i].Position) <= 3) {
            player.hudInteractString = player.incompleteTasks[i].Name;
            player.interactableTask = player.incompleteTasks[i];
            player.canTask = true;

            if (IsButtonHeld(player, Button.Interact)) {
                return;
            }
        }
    }
}


/* Find the nearest corpse and check if it is near enough to be reportable.
 */
void findNearestCorpse(ref define player) {
    define closestCorpseDistance = 10.0;
    define _closestCorpse;
    foreach (Corpse corpse in corpses) {
        define distance = DistanceBetween(PositionOf(player), corpse.Position);
        if (distance <= closestCorpseDistance) {
            _closestCorpse = corpse;
            closestCorpseDistance = distance;
        }
    }

    if (closestCorpseDistance <= 5.0) {
        player.closestCorpse = _closestCorpse;
        player.canReportCorpse = true;
        player.hudReloadString = "Report";
    } else {
        player.hudReloadString = "";
    }
}

/* Per-tick state update for all players, crew and imposters
 */
void playerUpdate(ref define player) {
    findNearestCorpse(player);
    debugUpdate(player);
}

rule: "Player: Interact (Report Corpse)"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (playing)
if (IsButtonHeld(EventPlayer(), Button.Reload))
if (EventPlayer().canReportCorpse)
{
    Corpse corpse = EventPlayer().closestCorpse;
    EventPlayer().canReportCorpse = false;
    CreateEffect(
        VisibleTo : AllPlayers(),
        Type      : Effect.EnergySound,
        Color     : Color.Purple,
        Position  : corpse.Position,
        Radius    : 5,
        Reevaluation: EffectRev.None
    );
    ModifyVariable(corpses, Operation.RemoveFromArrayByValue, corpse);
    DestroyEffect(corpse.Entity);
}
