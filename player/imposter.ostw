import "../main.ostw";

// Imposter variables
playervar define killCooldown = 0;
playervar define killBeamEffect;
playervar define killBeamEffectCountdown;
playervar define isVenting = false;
playervar define releasedUlt;

// Imposter team variables
globalvar define canSabotage;
globalvar define sabotageCooldown;

void createImposterTargetAura(ref define player) {
    CreateEffect(
        VisibleTo: (player.isAlive && player.role == PlayerRole.IMPOSTER && !player.isVenting && DistanceBetween(player, PlayerClosestToReticle(player)) <= KillDistance.Value) ? player : EmptyArray(),
        Type: Effect.GoodAura,
        Color: Color.Purple,
        Position: PlayerClosestToReticle(player),
        Radius: 0.75,
        Reevaluation: EffectRev.VisibleToPositionAndRadius
    );
}

void createImposterAura(ref define player) {
    CreateEffect(
        VisibleTo: FilteredArray(AllPlayers(), ArrayElement().role == PlayerRole.IMPOSTER && ArrayElement() != EventPlayer()),
        Type: Effect.BadAura,
        Color: Color.Purple,
        Radius: 0.75,
        Reevaluation: EffectRev.VisibleToPositionAndRadius,
        Position: EyePosition(EventPlayer())
    );
}

/* If target player is alive, kill them and return true.
 * Return false otherwise.
 */
define killPlayer(define target) "Kill Player" {
    if (IsAlive(target)) {
        target.corpse = new Corpse(target, PositionOf(target));
        target.hasCorpse = true;
        target.isAlive = false;
        return true;
    } else {
        return false;
    }
}

void resetSabotage() "Reset Sabotage" {
    sabotageCooldown = 0;
    canSabotage = true;
}

rule: "Global: Imposter Enable Sabotage"
Event.OngoingGlobal
if (sabotageCooldown == 0)
{
    canSabotage = true;
}

rule: "Player: Detect Ult button release"
Event.OngoingPlayer
if (!EventPlayer().releasedUlt)
if (!IsButtonHeld(EventPlayer(), Button.Ultimate))
{
    EventPlayer().releasedUlt = true;
}

rule: "Player: Imposter Interact (Vent)"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().role == PlayerRole.IMPOSTER)
if (IsButtonHeld(EventPlayer(), Button.Interact))
{
    define[] targetVents = FilteredArray(global_vents, DistanceBetween(ArrayElement(), EventPlayer()) <= VentInteractRadius);
    if (targetVents.Length == 0) {
        Wait(0.5);
        return;
    }

    EventPlayer().isVenting = !EventPlayer().isVenting;
    Wait(0.5);
}

rule: "Player: Imposter Venting"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().role == PlayerRole.IMPOSTER)
if (EventPlayer().isAlive)
if (EventPlayer().isVenting)
{
    StopChasingVariable(EventPlayer().killCooldown);
    SetInvisible(EventPlayer(), InvisibleTo.All);
    ForcePlayerHero(EventPlayer(), Hero.Zenyatta);
    SetMoveSpeed(EventPlayer(), 2 * MoveSpeed.Value);
    DisableMovementCollisionWithPlayers(EventPlayer());
}

rule: "Player: Imposter UnVenting"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().role == PlayerRole.IMPOSTER)
if (EventPlayer().isAlive)
if (!EventPlayer().isVenting)
{
    StopChasingVariable(EventPlayer().killCooldown);
    ChaseVariableAtRate(EventPlayer().killCooldown, 0, 1.0, RateChaseReevaluation.None);
    ForcePlayerHero(EventPlayer(), EventPlayer().originalHero);
    EnableMovementCollisionWithPlayers(EventPlayer());
    SetMoveSpeed(EventPlayer(), MoveSpeed.Value);
    SetInvisible(EventPlayer(), InvisibleTo.None);
}

rule: "Player: Destroy Kill Beam Effect"
Event.OngoingPlayer
if (EventPlayer().killBeamEffectCountdown == 0)
{
    DestroyEffect(EventPlayer().killBeamEffect);
}

rule: "Player: Imposter Interact (Kill)"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().isAlive)
if (EventPlayer().role == PlayerRole.IMPOSTER)
if (IsButtonHeld(EventPlayer(), Button.PrimaryFire))
if (!EventPlayer().isVenting)
if (EventPlayer().killCooldown == 0)
if (DistanceBetween(EventPlayer(), PlayerClosestToReticle(EventPlayer())) <= KillDistance.Value)
{
    
    define killTarget = PlayerClosestToReticle(EventPlayer());
    if (killTarget == null || !killTarget.isAlive || killTarget.role == PlayerRole.IMPOSTER) return;

    CreateBeamEffect(
        VisibleTo: AllPlayers(),
        BeamType: BeamType.BadBeam,
        Color: Color.Purple,
        StartPosition: EventPlayer(),
        EndPosition: killTarget,
        Reevaluation: EffectRev.None
    );
    
    EventPlayer().killBeamEffect = LastCreatedEntity();
    EventPlayer().killBeamEffectCountdown = 1;
    StopChasingVariable(EventPlayer().killBeamEffectCountdown);
    ChaseVariableAtRate(EventPlayer().killBeamEffectCountdown, 0, 1, RateChaseReevaluation.None);

    if (killPlayer(killTarget)) {
        EventPlayer().killCooldown = KillCooldown.Value;
        PlayEffect(
            VisibleTo: AllPlayers(),
            Type: PlayEffect.ExplosionSound,
            Position: PositionOf(EventPlayer()),
            Radius: 100
        );
        PlayEffect(
            VisibleTo: AllPlayers(),
            Type: PlayEffect.BadExplosion,
            Color: Color.Purple,
            Position: LinearInterpolate(PositionOf(killTarget), PositionOf(EventPlayer()), 0.5) + Vector(0, 1, 0),
            Radius: 1.0
        );
        checkEndGame();
    } else {
        EventPlayer().killBeamEffectCountdown = 0;
    }
}

rule: "Player: Imposter Play Init"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().role == PlayerRole.IMPOSTER)
{
    EventPlayer().killCooldown = KillCooldown.Value;
    StopChasingVariable(EventPlayer().killCooldown);
    ChaseVariableAtRate(EventPlayer().killCooldown, 0, 1.0, RateChaseReevaluation.None);
    EventPlayer().isVenting = false;
}
