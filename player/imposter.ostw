import "../main.ostw";

// Imposter variables
playervar define canKill = false;
playervar define killCooldown = 0;
playervar define killBeamEffect;
playervar define killBeamEffectCountdown;
playervar define canVent = false;
playervar define isVenting = false;
playervar define releasedUlt;

// Imposter team variables
globalvar define canSabotage;
globalvar define sabotageCooldown;

/* Per-tick state update for imposters.
 */
void imposterUpdate(ref define player) {
    player.hudInteractString = "";
    player.canVent = isVentNearby(player);
    if (player.canVent) {
        player.hudInteractString = "Vent";
    }
}

void createImposterTargetAura(ref define player) {
    CreateEffect(
        VisibleTo: (player.isAlive && player.role == PlayerRole.IMPOSTER && DistanceBetween(player, PlayerClosestToReticle(player)) <= KillDistance.Value) ? player : EmptyArray(),
        Type: Effect.GoodAura,
        Color: Color.Purple,
        Position: PlayerClosestToReticle(player),
        Radius: 0.75,
        Reevaluation: EffectRev.VisibleToPositionAndRadius
    );
    CreateEffect(
        VisibleTo: (player.isAlive && player.role == PlayerRole.IMPOSTER && DistanceBetween(player, PlayerClosestToReticle(player)) > KillDistance.Value) ? player : EmptyArray(),
        Type: Effect.GoodAura,
        Color: Color.White,
        Position: PlayerClosestToReticle(player),
        Radius: 0.75,
        Reevaluation: EffectRev.VisibleToPositionAndRadius
    );
}

void createImposterAura(ref define player) {
    CreateEffect(
        VisibleTo: FilteredArray(AllPlayers(), ArrayElement().role == PlayerRole.IMPOSTER && ArrayElement() != EventPlayer()),
        Type: Effect.BadAura,
        Color: Color.Red,
        Radius: 0.75,
        Reevaluation: EffectRev.VisibleToPositionAndRadius,
        Position: EyePosition(EventPlayer())
    );
}

define isVentNearby(ref define player) {
    foreach (Vector v in global_vents) {
        if (DistanceBetween(PositionOf(player), v) <= 2.0) {
            return true;
        }
    }
    return false;
}

/* If target player is alive, kill them and return true.
 * Return false otherwise.
 */
define killPlayer(define target) "Kill Player" {
    if (IsAlive(target)) {
        target.corpse = new Corpse(target, PositionOf(target));
        target.hasCorpse = true;
        target.isAlive = false;
        return true;
    } else {
        return false;
    }
}

void resetSabotage() "Reset Sabotage" {
    sabotageCooldown = 0;
    canSabotage = true;
}

rule: "Global: Imposter Enable Sabotage"
Event.OngoingGlobal
if (sabotageCooldown == 0)
{
    canSabotage = true;
}

rule: "Player: Detect Ult button release"
Event.OngoingPlayer
if (!EventPlayer().releasedUlt)
if (!IsButtonHeld(EventPlayer(), Button.Ultimate))
{
    EventPlayer().releasedUlt = true;
}

rule: "Player: Imposter Interact (Vent)"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().role == PlayerRole.IMPOSTER)
if (IsButtonHeld(EventPlayer(), Button.Interact))
if (EventPlayer().canVent)
{
    EventPlayer().canKill = false;
    EventPlayer().canVent = false;
    EventPlayer().isVenting = !EventPlayer().isVenting;
    Wait(0.5);
}

rule: "Player: Imposter Venting"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().role == PlayerRole.IMPOSTER)
if (EventPlayer().isAlive)
if (EventPlayer().isVenting)
{
    StopChasingVariable(EventPlayer().killCooldown);
    SetInvisible(EventPlayer(), InvisibleTo.All);
    ForcePlayerHero(EventPlayer(), Hero.Zenyatta);
    SetMoveSpeed(EventPlayer(), 2 * MoveSpeed.Value);
    DisableMovementCollisionWithPlayers(EventPlayer());
}

rule: "Player: Imposter UnVenting"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().role == PlayerRole.IMPOSTER)
if (EventPlayer().isAlive)
if (!EventPlayer().isVenting)
{
    StopChasingVariable(EventPlayer().killCooldown);
    ChaseVariableAtRate(EventPlayer().killCooldown, 0, 1.0, RateChaseReevaluation.None);
    ForcePlayerHero(EventPlayer(), EventPlayer().originalHero);
    EnableMovementCollisionWithPlayers(EventPlayer());
    SetMoveSpeed(EventPlayer(), MoveSpeed.Value);
    SetInvisible(EventPlayer(), InvisibleTo.None);
}

rule: "Player: Destroy Kill Beam Effect"
Event.OngoingPlayer
if (EventPlayer().killBeamEffectCountdown == 0)
{
    DestroyEffect(EventPlayer().killBeamEffect);
}

rule: "Player: Imposter Interact (Kill)"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().isAlive)
if (EventPlayer().role == PlayerRole.IMPOSTER)
if (IsButtonHeld(EventPlayer(), Button.PrimaryFire))
if (!EventPlayer().isVenting)
if (EventPlayer().killCooldown == 0)
if (DistanceBetween(EventPlayer(), PlayerClosestToReticle(EventPlayer())) <= KillDistance.Value)
{
    
    define killTarget = PlayerClosestToReticle(EventPlayer());
    if (killTarget == null || !killTarget.isAlive || killTarget.role != PlayerRole.CREW) return;

    EventPlayer().canVent = false;
    EventPlayer().canKill = false;

    CreateBeamEffect(
        VisibleTo: AllPlayers(),
        BeamType: BeamType.BadBeam,
        Color: Color.Purple,
        StartPosition: EventPlayer(),
        EndPosition: killTarget,
        Reevaluation: EffectRev.None
    );
    
    EventPlayer().killBeamEffect = LastCreatedEntity();
    EventPlayer().killBeamEffectCountdown = 1;
    StopChasingVariable(EventPlayer().killBeamEffectCountdown);
    ChaseVariableAtRate(EventPlayer().killBeamEffectCountdown, 0, 1, RateChaseReevaluation.None);

    if (killPlayer(killTarget)) {
        EventPlayer().killCooldown = KillCooldown.Value;
        PlayEffect(
            VisibleTo: AllPlayers(),
            Type: PlayEffect.ExplosionSound,
            Position: PositionOf(EventPlayer()),
            Radius: 100
        );
        PlayEffect(
            VisibleTo: AllPlayers(),
            Type: PlayEffect.BadExplosion,
            Color: Color.Purple,
            Position: LinearInterpolate(PositionOf(killTarget), PositionOf(EventPlayer()), 0.5) + Vector(0, 1, 0),
            Radius: 1.0
        );
        checkEndGame();
    } else {
        EventPlayer().killBeamEffectCountdown = 0;
    }
}

rule: "Player: Imposter Update Loop"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (EventPlayer().role == PlayerRole.IMPOSTER)
{
    EventPlayer().killCooldown = KillCooldown.Value;
    StopChasingVariable(EventPlayer().killCooldown);
    ChaseVariableAtRate(EventPlayer().killCooldown, 0, 1.0, RateChaseReevaluation.None);
    EventPlayer().canKill = false;
    EventPlayer().canVent = false;
    EventPlayer().isVenting = false;

    while (gameState == GameState.Play) {
        playerUpdate(EventPlayer());
        imposterUpdate(EventPlayer());
        renderUpdate(EventPlayer());
        Wait(TICK);
    }
}
