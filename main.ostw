import "customGameSettings.json";
import "player.ostw";
import "spawnBots.ostw";
import "tasks.ostw";
import "util.ostw";
import "hud.ostw";
import "lobby.ostw";
import "corpse.ostw";
import "imposter.ostw";
import "crewmate.ostw";
import "voting.ostw";

enum GameState {
    Waiting,
    Lobby,
    Start,
    Play,
    Voting
}

globalvar define TICK = 0.25;
globalvar GameState gameState = GameState.Waiting;
globalvar define started;
globalvar define playing;
globalvar define totalTaskCount;
globalvar define totalCompleteTaskCount;


rule: "Global: Setup"
{
    reset();
    DisableAnnouncer();
    DisableCompletion();
    DisableMusic();
    DisableScoring();

    SetMatchTime(0);
    PauseMatchTime();
}

rule: "Global: State Start"
if (gameState == GameState.Start)
{
    playing = true;
    markCenter();
    drawTasks();
    Wait(1);
    gameState = GameState.Play;
}

rule: "Player: State Start"
Event.OngoingPlayer
if (gameState == GameState.Start)
{
    if (EventPlayer().role == PlayerRole.IMPOSTER) {
        CreateEffect(
            VisibleTo: FilteredArray(AllLivingPlayers(), ArrayElement().role == PlayerRole.IMPOSTER && ArrayElement() != EventPlayer()),
            Type: Effect.BadAura,
            Color: Color.Red,
            Radius: 1,
            Reevaluation: EffectRev.VisibleToPositionAndRadius,
            Position: PositionOf(EventPlayer())
        );
    }
    putPlayerInCircle(EventPlayer());
    assignTasks(EventPlayer(), NumTasks.Value);
}

rule: "Global: Start Play"
if (gameState == GameState.Play)
{
    checkEndGame();
    drawGlobalHUD();
}

rule: "Player: State Play"
Event.OngoingPlayer
if (gameState == GameState.Play) {
    drawPlayerHUD(EventPlayer());
    updateHudStrings(EventPlayer());

    SetMoveSpeed(AllPlayers(), 100);
}

rule: "Start"
if (!started)
if (IsTrueForAll(AllPlayers(), HasSpawned(ArrayElement()) || IsDead(ArrayElement())))
{
    started = true;
    Wait(2);
    gameState = GameState.Lobby;
}

void reset() "Reset Game" {
    gameState = GameState.Waiting;
    AllPlayers().incompleteTasks = EmptyArray();
    AllPlayers().completeTasks = EmptyArray();
    AllPlayers().canKill = false;
    AllPlayers().canReportCorpse = false;
    AllPlayers().isTaskInProgress = false;
    corpses = EmptyArray();
    totalTaskCount = 0;
    totalCompleteTaskCount = 0;
    started = false;
    playing = false;
}

void endGame() "Play: End Game" {
    playing = false;
    SetMoveSpeed(AllPlayers(), 0);
    Wait(5);

    DestroyAllHudText();
    DestroyAllIcons();
    DestroyAllInworldText();
    DestroyAllDummyBots();
    DestroyAllEffects();
    reset();
}

void checkEndGame() "Play: Check End Game" {
    if (totalCompleteTaskCount >= totalTaskCount || CountOf(FilteredArray(AllLivingPlayers(), ArrayElement().role == PlayerRole.IMPOSTER)) == 0) {
        CreateHudText(
            VisibleTo   : AllPlayers(),
            Header      : "CREWMATES WIN!",
            HeaderColor : Color.Blue,
            Location    : Location.Top,
            Spectators  : Spectators.VisibleAlways,
            Reevaluation: HudTextRev.None,
            SortOrder   : 10
        );
        endGame();
    }
    if (CountOf(FilteredArray(AllLivingPlayers(), ArrayElement().role == PlayerRole.IMPOSTER))*2 >= CountOf(AllLivingPlayers())) {
        CreateHudText(
            VisibleTo   : AllPlayers(),
            Header      : "IMPOSTERS WIN!",
            HeaderColor : Color.Red,
            Location    : Location.Top,
            Spectators  : Spectators.VisibleAlways,
            Reevaluation: HudTextRev.None,
            SortOrder   : 10
        );
        endGame();
    }
}