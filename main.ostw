import "customGameSettings.json";
import "player/player.ostw";
import "player/pips.ostw";
import "player/imposter.ostw";
import "player/crewmate.ostw";
import "spawnBots.ostw";
import "tasks.ostw";
import "util.ostw";
import "hud.ostw";
import "lobby.ostw";
import "corpse.ostw";
import "voting.ostw";
import "vents.ostw";
import "emergencyButton.ostw";
import "sabotage.ostw";

enum GameState {
    Waiting,
    Lobby,
    Start,
    Play,
    Voting
}

globalvar define TICK = 0.25;
globalvar GameState gameState = GameState.Waiting;
globalvar define started;
globalvar define playing;
globalvar define totalTaskCount;
globalvar define totalCompleteTaskCount;


rule: "Global: Setup"
{
    DisableInspectorRecording();
    reset();
    DisableAnnouncer();
    DisableCompletion();
    DisableMusic();
    DisableScoring();

    SetMatchTime(0);
    PauseMatchTime();
}

rule: "Global: State Start"
if (gameState == GameState.Start)
{
    playing = true;
    drawTasks();
    drawVents();
    drawEmergencyButton();
    Wait(1);
    gameState = GameState.Play;
}

rule: "Global: Start Play"
if (gameState == GameState.Play)
{
    checkEndGame();
    drawGlobalHUD();
    SetMoveSpeed(FilteredArray(AllPlayers(), ArrayElement().isAlive), MoveSpeed.Value);
    SetMoveSpeed(FilteredArray(AllPlayers(), !ArrayElement().isAlive), 2 * MoveSpeed.Value);
    StopForcingThrottle(AllPlayers());
}

rule: "Player: State Play"
Event.OngoingPlayer
if (gameState == GameState.Play) {
    drawPlayerHUD(EventPlayer());
    updateHudStrings(EventPlayer());
    resetSabotage();

    DestroyEffect(EventPlayer().deathAura);
}

rule: "Start"
if (gameState == GameState.Waiting)
if (!started)
if (IsTrueForAll(AllPlayers(), HasSpawned(ArrayElement()) || IsDead(ArrayElement())))
{
    started = true;
    Wait(2);
    gameState = GameState.Lobby;
}

void reset() "Reset Game" {
    AllPlayers().incompleteTasks = EmptyArray();
    AllPlayers().completeTasks = EmptyArray();
    AllPlayers().canKill = false;
    AllPlayers().canVent = false;
    AllPlayers().closestCorpse = null;
    AllPlayers().isTaskInProgress = false;
    AllPlayers().isVenting = false;
    AllPlayers().hasCorpse = false;
    AllPlayers().hasButtonPress = true;
    totalTaskCount = 0;
    totalCompleteTaskCount = 0;

    AllPlayers().isAlive = true;

    playing = false;
    started = false;
}

void endGame() "Play: End Game" {
    CleanUpSabotage();
    gameState = GameState.Waiting;
    playing = false;
    SetMoveSpeed(AllPlayers(), 0);
    Wait(4);

    foreach (define player in AllPlayers()) {
        if (!player.isAlive) {
            ForcePlayerHero(player, player.originalHero);
            EnableMovementCollisionWithPlayers(player);
            SetInvisible(player, InvisibleTo.None);
        }
    }

    DestroyAllHudText();
    DestroyAllIcons();
    DestroyAllInworldText();
    DestroyAllDummyBots();
    DestroyAllEffects();

    reset();
}

define renderImposterList() "Render Imposter List" {
    define _renderedImposterList = "Imposter(s) were:\n";
    foreach (define imp in FilteredArray(AllPlayers().role == PlayerRole.IMPOSTER)) {
        _renderedImposterList = <"<0> <1><2>",
            _renderedImposterList,
            availablePips[SlotOf(imp)],
            IsDummyBot(imp) ? imp.originalHero : imp
        >;
    }
    return _renderedImposterList;
}

void imposterVictory() "Imposter Victory" {
    BigMessage(AllPlayers(), "Imposters Win!");
    define renderedImposterList = renderImposterList();
    CreateHudText(
        VisibleTo   : AllPlayers(),
        Header      : <"IMPOSTERS WIN!\n\n<0>", renderedImposterList>,
        HeaderColor : Color.Red,
        Location    : Location.Top,
        Spectators  : Spectators.VisibleAlways,
        Reevaluation: HudTextRev.None,
        SortOrder   : 10
    );
    endGame();
}

void crewVictory() "Crew Victory" {
    BigMessage(AllPlayers(), "Crewmates Win!");
    define renderedImposterList = renderImposterList();
    CreateHudText(
        VisibleTo   : AllPlayers(),
        Header      : <"CREWMATES WIN!\n\n<0>", renderedImposterList>,
        HeaderColor : Color.Blue,
        Location    : Location.Top,
        Spectators  : Spectators.VisibleAlways,
        Reevaluation: HudTextRev.None,
        SortOrder   : 10
    );
    endGame();
}

void foolVictory() "Fool Victory" {
    BigMessage(AllPlayers(), "Fool Wins!");
    CreateHudText(
        VisibleTo   : AllPlayers(),
        Header      : <"FOOL WINS!\n\nFool was:\n<0>", SafePlayerName(FirstOf(FilteredArray(AllPlayers(), ArrayElement().role == PlayerRole.FOOL)))>,
        HeaderColor : Color.Yellow,
        Location    : Location.Top,
        Spectators  : Spectators.VisibleAlways,
        Reevaluation: HudTextRev.None,
        SortOrder   : 10
    );
    endGame();
}

void checkEndGame() "Play: Check End Game" {
    if (playing) {
        if (totalCompleteTaskCount >= totalTaskCount || CountOf(AllAliveImposters()) == 0) {
            crewVictory();
        }
        if ((2 * CountOf(AllAliveImposters())) >= CountOf(AllAlive())) {
            imposterVictory();
        }
    }
}