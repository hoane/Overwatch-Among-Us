import "customGameSettings.json";
import "player.ostw";
import "spawnBots.ostw";
import "tasks.ostw";
import "util.ostw";

enum GameState {
    Waiting,
    Play
}

globalvar GameState gameState = GameState.Waiting;
globalvar define started = false;


rule: "Global: Setup"
{
    DisableAnnouncer();
    DisableCompletion();
    DisableMusic();
    DisableScoring();

    SetMatchTime(0);
    PauseMatchTime();
    spawnBots(9);
}

rule: "Global: Start Play"
if (gameState == GameState.Play)
{
    markCenter();
    drawTasks();
    assignRoles(2);

    CreateHudText(
        VisibleTo  : FilteredArray(AllPlayers(), ArrayElement().role == PlayerRole.CREW),
        Header     : "CREWMATE",
        HeaderColor: Color.SkyBlue,
        Location   : Location.Right,
        SortOrder  : 0,
        Spectators : Spectators.VisibleNever,
        Reevaluation: HudTextRev.None
    );
    CreateHudText(
        VisibleTo  : FilteredArray(AllPlayers(), ArrayElement().role == PlayerRole.IMPOSTER),
        Header     : "IMPOSTER",
        HeaderColor: Color.Red,
        Location   : Location.Right,
        SortOrder  : 0,
        Spectators : Spectators.VisibleNever,
        Reevaluation: HudTextRev.None
    );
}

rule: "Player: Start Play"
Event.OngoingPlayer
if (gameState == GameState.Play)
{
    putPlayerInCircle(EventPlayer());

    CreateInWorldText(
        AllPlayers(),
        EventPlayer().role == PlayerRole.CREW ? "CREW" : "IMPOSTER", PositionOf(EventPlayer()), 4, Clipping.ClipAgainstSurfaces,
        InworldTextRev.VisibleToPositionAndString,
        Color.White
    );

    assignTasks(EventPlayer(), 2);
    drawTasksHUD(EventPlayer(), 2);
}


rule: "Start"
if (!started)
if (IsTrueForAll(AllPlayers(), HasSpawned(ArrayElement()) || IsDead(ArrayElement())))
{
    started = true;
    Wait(10);
    gameState = GameState.Play;
}