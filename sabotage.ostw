import "main.ostw";

// Sabotage Menu

enum SabotageType {
    None = -1,
    OverloadPower = 0,
    DisableComms = 1
}

globalvar PlayerMenuOption[] sabotageOptions = [
    new MenuText("Select Sabotage"),
    new SabotageButton("Overload Power Transformers", 0, SabotageType.OverloadPower, 60),
    new SabotageButton("Disable Communications", 0, SabotageType.DisableComms, 30)
];

playervar define isSabotageMenuOpen;
playervar define renderedSabotageMenu;
playervar define sabotageMenuSelected;
playervar define sabotageMenuTextId;

rule: "Player: Imposter Ult (Sabotage)"
Event.OngoingPlayer
if (gameState == GameState.Play)
if (playing)
if (canSabotage)
if (EventPlayer().role == PlayerRole.IMPOSTER)
if (!EventPlayer().isSabotageMenuOpen)
if (EventPlayer().releasedUlt)
if (IsButtonHeld(EventPlayer(), Button.Ultimate))
{
    EventPlayer().releasedUlt = false;
    EventPlayer().isSabotageMenuOpen = true;
    EventPlayer().sabotageMenuSelected = 1;
    //SetMoveSpeed(EventPlayer(), 0);

    CreateHudText(
        VisibleTo   : EventPlayer(),
        Header      : EventPlayer().renderedSabotageMenu,
        HeaderColor : Color.Purple,
        Text        : sabotageOptions[EventPlayer().sabotageMenuSelected].Controls,
        TextColor   : Color.Purple,
        Location    : Location.Top,
        Spectators  : Spectators.VisibleNever,
        Reevaluation: HudTextRev.String,
        SortOrder   : 1
    );
    EventPlayer().sabotageMenuTextId = LastTextID();
    
    RenderSabotageMenu(EventPlayer());
}

rule: "Sabotage Menu: Ult (Close Menu)"
Event.OngoingPlayer
if (EventPlayer().isSabotageMenuOpen)
if (EventPlayer().releasedUlt)
if (IsButtonHeld(EventPlayer(), Button.Ultimate))
{
    releasedUlt = false;
    EventPlayer().isSabotageMenuOpen = false;
}

void RenderSabotageMenu(define player) "Sabotage: Render Menu" {
    player.renderedSabotageMenu = "";
    for (define i = 0; i < sabotageOptions.Length; i++) {
        player.renderedSabotageMenu = <"<0><1><2>",
            player.renderedSabotageMenu,
            sabotageOptions[i].RenderedLine(i == player.sabotageMenuSelected),
            i < sabotageOptions.Length - 1 ? "\n" : ""
        >;
    }
}

rule: "Sabotage Menu: Disable menu when not available"
Event.OngoingPlayer
if (gameState != GameState.Play || !canSabotage)
if (EventPlayer().isSabotageMenuOpen)
{
    EventPlayer().isSabotageMenuOpen = false;
}

rule: "Sabotage Menu: Destroy Menu"
Event.OngoingPlayer
if (!EventPlayer().isSabotageMenuOpen)
{
    DestroyHudText(EventPlayer().sabotageMenuTextId);
}

rule: "Sabotage Menu: Previous Option"
Event.OngoingPlayer
if (EventPlayer().isSabotageMenuOpen)
if (ThrottleOf(EventPlayer()).Z > 0)
{
    EventPlayer().sabotageMenuSelected--;
    if (EventPlayer().sabotageMenuSelected < 1) EventPlayer().sabotageMenuSelected = sabotageOptions.Length - 1;
    RenderSabotageMenu(EventPlayer());
}

rule: "Sabotage Menu: Next Option"
Event.OngoingPlayer
if (EventPlayer().isSabotageMenuOpen)
if (ThrottleOf(EventPlayer()).Z < 0)
{
    EventPlayer().sabotageMenuSelected++;
    if (EventPlayer().sabotageMenuSelected >= sabotageOptions.Length) EventPlayer().sabotageMenuSelected = 1;
    RenderSabotageMenu(EventPlayer());
}

rule: "Voting Menu: Press"
Event.OngoingPlayer
if (canSabotage)
if (EventPlayer().isSabotageMenuOpen)
if (IsButtonHeld(EventPlayer(), Button.Interact))
{
    sabotageOptions[EventPlayer().sabotageMenuSelected].OnSelect();
}

class SabotageButton : PlayerMenuOption {

    public define Sabotage;
    public define Cooldown;
    
    public override define Controls: <"Press <0> to sabotage.", InputBindingString(ButtonValue(Button.Interact))>;

    public SabotageButton(ref define name, ref define value, ref define sabotage, ref define cooldown)
    {
        Name = name;
        Value = value;
        Sabotage = sabotage;
        Cooldown = cooldown;
    }

    public override define RenderedLine(define selected): <"<1><0>", Name, selected ? "▶" : "▷">;

    public override void OnSelect()
    {
        root.canSabotage = false;
        EventPlayer().isSabotageMenuOpen = false;
        root.sabotageCooldown = this.Cooldown == 0 ? root.SabotageCooldown.Value : this.Cooldown;
        StopChasingVariable(root.sabotageCooldown);
        ChaseVariableAtRate(root.sabotageCooldown, 0, 1, RateChaseReevaluation.None);
        SetMoveSpeed(root.MoveSpeed.Value);

        root.sabotageInProgress = this.Sabotage;
        root.StartSabotageEvent();
    }
}

// Sabotage Events

globalvar define sabotageInProgress = -1;
globalvar define sabotageInProgressHeader = "";
globalvar define sabotageInProgressValue = "";
globalvar define sabotageInteractString = "";
globalvar define[] sabotageLocations;
globalvar define[] sabotageEntityIds;
globalvar define[] sabotageTextIds;

void StartSabotageEvent() {
    if (sabotageInProgress == SabotageType.OverloadPower) {
        startPowerOverloadSabotageEvent();
    } else if (sabotageInProgress == SabotageType.DisableComms) {
        startDisableCommunicationsSabotageEvent();
    } else {
        sabotageInProgress = SabotageType.None;
    }
}

void CleanUpSabotage() "Sabotage: Clean Up" {
    sabotageInProgress = -1;
    sabotageLocations = null;
    sabotageInProgressHeader = "";
    sabotageInProgressValue = "";

    foreach (define id in sabotageEntityIds) {
        DestroyEffect(id);
    }
    foreach (define id in sabotageTextIds) {
        DestroyInWorldText(id);
    }
    sabotageEntityIds = [];
    sabotageTextIds = [];
}

define getNearestSabotageLocation(ref define player) {
    define closestSabotageDistance = 10.0;
    define _closestSabotage = null;
    if (sabotageInProgress >= 0) {
        foreach (Vector v in sabotageLocations) {
            define distance = DistanceBetween(player, v);
            if (distance <= closestSabotageDistance) {
                _closestSabotage = v;
                closestSabotageDistance = distance;
            }
        }
    }

    return closestSabotageDistance <= 5 ? _closestSabotage : null;
}

void fixSabotageAt(ref Vector v) {
    if (sabotageInProgress == SabotageType.OverloadPower) {
        fixTransformerBoxAt(v);
    } else if (sabotageInProgress == SabotageType.DisableComms) {
        fixCommsAt(v);
    }
}

rule: "Sabotage: Fail"
if (gameState == GameState.Play)
if (playing)
if (sabotageInProgress == SabotageType.OverloadPower)
if (sabotageInProgressValue == 0)
{
    imposterVictory();
}

// Power Overload

globalvar Vector[] TransformerBoxes = [
    Vector(-167.03, 6.79, -38.47),
    Vector(-190.25, -2.86, -68.97)
];
globalvar define[] transformerBoxComplete;

void startPowerOverloadSabotageEvent() {
    sabotageInProgressHeader = "Power Overload: Reset Circuit Breakers!";
    sabotageInteractString = "Reset Circuit Breaker";
    StopChasingVariable(sabotageInProgressValue);
    ChaseVariableAtRate(sabotageInProgressValue, 0, 1, RateChaseReevaluation.None);
    sabotageInProgressValue = 60;

    transformerBoxComplete = [false, false];
    sabotageLocations = TransformerBoxes;

    drawTransformerBox(0);
    drawTransformerBox(1);
}

void fixTransformerBoxAt(ref Vector v) {
    define idx = IndexOfArrayValue(TransformerBoxes, v)
    if (idx == -1) return;
    transformerBoxComplete[idx] = true;

    PlayEffect(
        VisibleTo: AllPlayers(),
        Type: PlayEffect.RingExplosionSound,
        Position: PositionOf(EventPlayer()),
        Radius: 100
    );

    if (IsTrueForAll(transformerBoxComplete, ArrayElement() == true)) {
        CleanUpSabotage();
    }
}

void drawTransformerBox(ref define idx) {
    CreateEffect(
        VisibleTo: transformerBoxComplete[idx] ? EmptyArray() : AllPlayers(),
        Type: Effect.Sphere,
        Color: Color.Red,
        Position: TransformerBoxes[idx],
        Radius: 0.75,
        Reevaluation: EffectRev.VisibleTo
    );
    ModifyVariable(sabotageEntityIds, Operation.AppendToArray, LastCreatedEntity());

    CreateInWorldText(
        VisibleTo: transformerBoxComplete[idx] ? EmptyArray() : FilteredArray(AllPlayers(), DistanceBetween(ArrayElement(), TransformerBoxes[idx]) > 5),
        Header: "!",
        Position: TransformerBoxes[idx],
        Scale: 5,
        Clipping: Clipping.DoNotClip,
        Reevaluation: InworldTextRev.VisibleTo,
        TextColor: Color.Red
    );
    ModifyVariable(sabotageTextIds, Operation.AppendToArray, LastTextID());

    CreateInWorldText(
        VisibleTo: transformerBoxComplete[idx] ? EmptyArray() : PlayersWithinRadius(TransformerBoxes[idx], 5),
        Header: "Reset Circuit Breaker",
        Position: TransformerBoxes[idx],
        Scale: 4,
        Clipping: Clipping.DoNotClip,
        Reevaluation: InworldTextRev.VisibleTo,
        TextColor: Color.Red
    );
    ModifyVariable(sabotageTextIds, Operation.AppendToArray, LastTextID());
}

// Disable Communications

globalvar Vector CommunicationsHub = Vector(-217.24, 2.0, 6.90);

void startDisableCommunicationsSabotageEvent() {
    sabotageInProgressHeader = "Communications Disabled: Reprogram Comms Hub!";
    sabotageInteractString = "Reprogram Comms Hub";
    sabotageLocations = [CommunicationsHub];
    ModifyVariable(sabotageLocations, Operation.AppendToArray, CommunicationsHub);

    StopChasingVariable(sabotageInProgressValue);
    sabotageInProgressValue = " ";

    foreach (define p in AllPlayers()) {
        updateHudStrings(p);
    }

    drawCommunicationsHub();
}

void drawCommunicationsHub() {
    CreateEffect(
        VisibleTo: AllPlayers(),
        Type: Effect.Sphere,
        Color: Color.Red,
        Position: CommunicationsHub,
        Radius: 0.75,
        Reevaluation: EffectRev.None
    );
    ModifyVariable(sabotageEntityIds, Operation.AppendToArray, LastCreatedEntity());

    CreateInWorldText(
        VisibleTo: FilteredArray(AllPlayers(), DistanceBetween(ArrayElement(), CommunicationsHub) > 5),
        Header: "!",
        Position: CommunicationsHub,
        Scale: 5,
        Clipping: Clipping.DoNotClip,
        Reevaluation: InworldTextRev.VisibleTo,
        TextColor: Color.Red
    );
    ModifyVariable(sabotageTextIds, Operation.AppendToArray, LastTextID());

    CreateInWorldText(
        VisibleTo: PlayersWithinRadius(CommunicationsHub, 5),
        Header: "Reprogram Comms Hub",
        Position: CommunicationsHub,
        Scale: 4,
        Clipping: Clipping.DoNotClip,
        Reevaluation: InworldTextRev.VisibleTo,
        TextColor: Color.Red
    );
    ModifyVariable(sabotageTextIds, Operation.AppendToArray, LastTextID());
}

void fixCommsAt(ref Vector v) {
    PlayEffect(
        VisibleTo: AllPlayers(),
        Type: PlayEffect.RingExplosionSound,
        Position: PositionOf(EventPlayer()),
        Radius: 100
    );
    CleanUpSabotage();
}